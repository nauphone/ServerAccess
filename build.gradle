plugins {
    id 'java'
    id 'sonar-runner'
    id 'info.solidsoft.pitest' version '1.1.1'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

group = 'ru.naumen'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def detectVersion() {
    try {
        def rawVersion = 'git describe'.execute().text.trim()
        def extractedVersion = rawVersion =~ /serveraccess-([0-9.]*(-[0-9]+)?)/
        return extractedVersion[0][1]
    } catch (e) {
        return 'devel'
    }
}

def currentVersion = detectVersion()

repositories {
    mavenCentral()
}

dependencies {
    compile (
        files('lib/mindterm.jar', 'lib/swt.jar'),
        'org.apache.httpcomponents:httpclient:4.2.5',
        'log4j:log4j:1.2.17',
    )
    testCompile (
        'junit:junit:4.11',
        'org.hamcrest:hamcrest-all:1.3',
    )
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.naumen.servacc.ui.Main'
    }
}

import com.github.jengelman.gradle.plugins.shadow.internal.DefaultDependencyFilter

class WinDependencyFilter extends DefaultDependencyFilter {
    private FileCollection exclude
    private FileCollection include
    
    WinDependencyFilter(Project project, FileCollection exclude, FileCollection include) {
        super(project)
        this.exclude = exclude
        this.include = include
    }
    
    FileCollection resolve(Configuration configuration) {
        FileCollection fc = super.resolve(configuration)
        return (fc - this.exclude) + this.include
    }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task shadowJarWin64(type: ShadowJar) {
    configureShadowJarWin(shadowJarWin64, 'win64')
}

task shadowJarWin32(type: ShadowJar) {
    configureShadowJarWin(shadowJarWin32, 'win32')
}

def configureShadowJarWin(ShadowJar shadowJarWin, String platform) {
    shadowJarWin.description = shadowJar.description + ' for ' + platform + ' platform'
    shadowJarWin.conventionMapping.with {
        map('classifier') {
            'all'
        }
    }
    shadowJarWin.destinationDir = file(shadowJar.destinationDir.getPath() + '/' + platform)
    shadowJarWin.manifest.from shadowJar.manifest
    shadowJarWin.from sourceSets.main.output
    shadowJarWin.configurations = [project.configurations.runtime.copyRecursive()]
    shadowJarWin.exclude 'META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    shadowJarWin.dependencyFilter = new WinDependencyFilter(getProject(), files('lib/swt.jar'), platform == 'win64' ? files('lib/win_64/swt.jar') : files('lib/win_32/swt.jar'))
}

sonarRunner {
    sonarProperties {
        property "sonar.jdbc.url", "jdbc:postgresql://localhost:5432/sonar"
        property "sonar.jdbc.driverClassName", "org.postgresql.Driver"
        property "sonar.projectVersion", currentVersion
    }
}

task prepareRelease {dependsOn build, shadowJarWin64, shadowJarWin32} << {
    copy {
        from 'resource/server-access'
        from 'resource/log4j.properties'
        into 'build/libs'
    }
}

task win32(dependsOn: prepareRelease) << {
    exec {
        workingDir 'nsis'
        commandLine 'makensis', '-DArch=32', '-DbuildVersion=' + currentVersion, 'serveraccess2.nsi'
        standardOutput = new FileOutputStream('build/nsis.out')
        errorOutput = new FileOutputStream('build/nsis.err')
    }
}

task win64(dependsOn: prepareRelease) << {
    exec {
        workingDir 'nsis'
        commandLine 'makensis', '-DArch=64', '-DbuildVersion=' + currentVersion, 'serveraccess2.nsi'
        standardOutput = new FileOutputStream('build/nsis.out')
        errorOutput = new FileOutputStream('build/nsis.err')
    }
}

task macosx {dependsOn build, shadowJar} << {
    copy {
        from zipTree('resource/ServerAccess.app.tmpl.zip')
        into 'build/libs'
    }
    copy {
        from configurations.runtime
        into 'build/libs/ServerAccess.app/Contents/MacOS'
    }
    copy {
        from 'resource/log4j.properties'
        from 'build/libs/ServerAccess-all.jar'
        from 'resource/scripts/serveraccess'
        into 'build/libs/ServerAccess.app/Contents/MacOS'
    }
    copy {
        from 'lib/macosx/swt.jar'
        into 'build/libs/ServerAccess.app/Contents/MacOS/swt'
    }
}

// vim: set ft=groovy:
